plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'application'
}

group 'org.example'
version '1.0'

sourceCompatibility = 1.11
targetCompatibility = 1.11
mainClassName = 'info.ernestas.webservice.WebServiceApplication'

shadowJar {
    archiveFileName = 'webservice-all.jar'
}

repositories {
    mavenCentral()
}

ext {
    slf4jVersion = '1.7.28'
    lombokVersion = '1.18.12'

    hamcrest = '2.1'
    junitJupiter = '5.5.2'
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:slf4j-simple:${slf4jVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'

    testCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '2.2.3.RELEASE'
    testCompile "org.hamcrest:hamcrest:${hamcrest}"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiter}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiter}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiter}"
}

configurations.all {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
}

test {
    useJUnitPlatform()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java {
            srcDir file('src/intTest/java')
        }
        resources.srcDir file('src/intTest/resources')
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest
